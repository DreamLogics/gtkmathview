dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/MathMLObject.cc)

MATHVIEW_MAJOR_VERSION=0
MATHVIEW_MINOR_VERSION=2
MATHVIEW_MICRO_VERSION=9
MATHVIEW_VERSION=$MATHVIEW_MAJOR_VERSION.$MATHVIEW_MINOR_VERSION.$MATHVIEW_MICRO_VERSION
MATHVIEW_VERSION_INFO=`expr $MATHVIEW_MAJOR_VERSION + $MATHVIEW_MINOR_VERSION`:$MATHVIEW_MICRO_VERSION:$MATHVIEW_MINOR_VERSION

MAJOR_VERSION=$MATHVIEW_MAJOR_VERSION
VERSION=$MATHVIEW_VERSION

AC_SUBST(MAJOR_VERSION)
AC_SUBST(MATHVIEW_VERSION)
AC_SUBST(MATHVIEW_VERSION_INFO)

AC_ARG_ENABLE(
	profile,
	[  --enable-profile[=ARG]  include profiling information [default=no]],
	profile=$enableval,
	profile=no
)

AC_ARG_ENABLE(
	debug,
	[  --enable-debug[=ARG]    include debugging debug [default=yes]],
	[
		if test $enableval = yes; then
			AC_DEFINE(ENABLE_DEBUG)
		fi
	],
	AC_DEFINE(ENABLE_DEBUG)
)

AC_ARG_ENABLE(
	floating,
	[  --enable-floating[=ARG] use floating point arithmentics for internal
                          computations [default=yes]],
	[
		if test $enableval = yes; then
			AC_DEFINE(FLOATING_SCALED)
		fi
	],
	AC_DEFINE(FLOATING_SCALED)
)

AC_ARG_ENABLE(
	texish,
	[  --enable-texish[=ARG]   enable TeX like layout when possible [default=no]],
	[
		if test $enableval = yes; then
			AC_DEFINE(TEXISH_MATHML)
		fi
	],
)

AC_ARG_ENABLE(
	extensions,
	[  --enable-extensions[=ARG] enable MathML extensions [default=yes]],
	[
		if test $enableval = yes; then
			AC_DEFINE(ENABLE_EXTENSIONS)
		fi
	],
	AC_DEFINE(ENABLE_EXTENSIONS)
)

AC_ARG_WITH(
	t1lib,
	[  --with-t1lib[=ARG]      compile with t1lib 1.x library [default=auto]],
	t1lib=$withval,
	t1lib=auto
)

AC_ARG_WITH(
        t1lib-prefix,
        [  --with-t1lib-prefix=PFX prefix dir where t1lib is installed],
        [
                with_t1lib_prefix=yes
                T1LIB_PREFIX=$withval
        ],
        [
                with_t1lib_prefix=no
        ]
)

AC_ARG_WITH(
	glib-config,
	[  --with-glib-config[=ARG] use glib-config to determine glib configuration],
	with_glib_config=$withval,
	with_glib_config=no
)

AC_ARG_ENABLE(
	export-PS,
	[  --enable-export-PS[=ARG] enable support for exporting PostScript [default=auto]],
	export_PS=$enableval,
	export_PS=auto
)

dnl Automake configuration
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gtkmathview, $MATHVIEW_VERSION)
dnl AC_DISABLE_SHARED
AM_PROG_LIBTOOL

dnl Checks for programs.
AC_LANG_C
AC_PROG_CC
AC_PROG_CXX
AC_PROG_LN_S
AC_ISC_POSIX
AC_CHECK_PROG(HAVE_COLORGCC, colorgcc, yes, no)
AC_CHECK_PROG(HAVE_ICONV, iconv, yes, no)
AC_CHECK_PROG(HAVE_GLIB_CONFIG, glib-config, yes, no)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(wctype.h,,AC_MSG_WARN(could not find required header file wctype.h))
AC_CHECK_HEADERS(wchar.h,,AC_MSG_WARN(could not find required header file wchar.h))
AC_CHECK_HEADERS(getopt.h)
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADER(iconv.h,,AC_MSG_ERROR(could not find required header file iconv.h))

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_CHAR_UNSIGNED
AC_C_BIGENDIAN

dnl Check for iconv and code names
AC_MSG_CHECKING(for iconv encodings)
if test $HAVE_ICONV = "yes"; then
	if iconv -f UTF-8 -t UCS-4 /dev/null; then
		AC_DEFINE(ICONV_UTF8, "UTF-8")
		AC_DEFINE(ICONV_UCS4, "UCS-4")
		AC_MSG_RESULT(UTF-8 ==> UCS-4)
	elif iconv -f UTF8 -t UCS-4 /dev/null; then
		AC_DEFINE(ICONV_UTF8, "UTF8")
		AC_DEFINE(ICONV_UCS4, "UCS-4")
		AC_MSG_RESULT(UTF8 ==> UCS-4)
	elif iconv -f UTF-8 -t UCS4; then
		AC_DEFINE(ICONV_UTF8, "UTF-8")
		AC_DEFINE(ICONV_UCS4, "UCS4")
		AC_MSG_RESULT(UTF-8 ==> UCS4)
	elif iconv -f UTF8 -t UCS4; then
		AC_DEFINE(ICONV_UTF8, "UTF8")
		AC_DEFINE(ICONV_UCS4, "UCS4")
		AC_MSG_RESULT(UTF8 ==> UCS4)
	else
		AC_DEFINE(ICONV_UTF8, "UTF-8")
		AC_DEFINE(ICONV_UCS4, "UCS-4")
		AC_MSG_RESULT(not found, using default UTF-8 ==> UCS-4)
	fi
else	
	AC_DEFINE(ICONV_UTF8, "UTF-8")
	AC_DEFINE(ICONV_UCS4, "UCS-4")
	AC_MSG_RESULT(not found, using default UTF-8 ==> UCS-4)
fi

dnl Check for iconv with const pointer as second argument
AC_LANG_C
AC_LANG_CPLUSPLUS
AC_MSG_CHECKING(whether iconv has a const argument)
AC_TRY_COMPILE(
	[#include <iconv.h>],
	[
		const char* inbuf;
		iconv_t     cd;
		iconv(cd, &inbuf, 0, 0, 0);
	],
	[
		AC_DEFINE(ICONV_CONST)
		AC_MSG_RESULT(yes)
	],
	[
		AC_MSG_RESULT(no)
	]
)
AC_LANG_RESTORE

dnl Checks for libraries
AC_CHECK_LIB(iberty, getopt_long,
	LIBS="$LIBS -liberty",
	,
	-liberty
)
AC_CHECK_FUNC(getopt_long, AC_DEFINE(HAVE_GETOPT_LONG))

AC_LANG_C
if test $with_glib_config = "no"; then
	AM_PATH_GLIB(1.2.0,
		[
			CFLAGS="$CFLAGS $GLIB_CFLAGS"
			CXXFLAGS="$CXXFLAGS $GLIB_CFLAGS"
			LIBS="$LIBS $GLIB_LIBS"
		],
		AC_MSG_ERROR(could not find glib)
	)
else
	if test $HAVE_GLIB_CONFIG = "yes"; then
		CFLAGS="$CFLAGS `glib-config --cflags`"
		CXXFLAGS="$CXXFLAGS `glib-config --cflags`"
		LIBS="$LIBS `glib-config --libs`"
	else
		AC_MSG_ERROR(could not find glib)
	fi
fi

AM_PATH_GTK(1.2.0, gtklib="yes", gtklib="no")

if test $gtklib = "yes"; then
	CFLAGS="$CFLAGS $GTK_CFLAGS"
	CXXFLAGS="$CXXFLAGS $GTK_CFLAGS"
	LIBS="$LIBS $GTK_LIBS"
else
	AC_MSG_WARN(GTK is needed in order to use the widget),
fi

AC_CHECK_LIB(minidom, mdom_node_get_type,
	[
		CFLAGS="$CFLAGS `minidom-config --cflags`"
		CXXFLAGS="$CXXFLAGS `minidom-config --cflags`"
		LIBS="$LIBS `minidom-config --libs`"
	],
	AC_MSG_ERROR(gtkmathview will not compile if minidom is not installed),
	`minidom-config --libs` `glib-config --libs`
)

vers=`minidom-config --version | awk -F. '{ printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
minvers=`echo 0.1.3 | awk -F. '{ printf "%d", ($1 * 1000 + $2) * 1000 + $3;}'`
if test "$vers" -lt "$minvers"; then
        AC_MSG_ERROR(you need at least minidom 0.1.3)
fi

if test $t1lib = "auto"; then
	AC_CHECK_LIB(t1, T1_InitLib, t1lib="yes", t1lib="no")
	AC_CHECK_HEADER(t1lib.h, , t1lib="no")
fi

if test $t1lib = "yes"; then
	LIBS="$LIBS -lt1x -lt1"
	AC_DEFINE(HAVE_LIBT1)

        if test $with_t1lib_prefix = "yes"; then
                CFLAGS="-I$T1LIB_PREFIX/include $CFLAGS"
                CXXFLAGS="-I$T1LIB_PREFIX/include $CXXFLAGS"
        fi
fi

if test "(" $t1lib = "no" ")" -a "(" $export_PS = "yes" ")"; then
	AC_MSG_WARN(cannot export to PostScript if t1lib is not available)
	export_PS=no
fi

if test "(" $export_PS = "auto" ")"; then
	export_PS=yes
fi

if test $HAVE_COLORGCC = "yes"; then
	CC=colorgcc
	CXX=colorgcc
fi

CFLAGS="$CFLAGS -W -Wall"
CXXFLAGS="$CXXFLAGS -W -Wall"

if test $profile = yes; then
	CFLAFS="$CFLAGS -p"
	CXXFLAGS="$CXXFLAGS -p"
	AC_DEFINE(ENABLE_PROFILE)
fi

AC_OUTPUT([
Makefile
gtkmathview.spec
config/Makefile
config/math-engine-configuration.xml
scripts/Makefile
src/Makefile
auto/Makefile
auto/config.dirs
doc/Makefile
viewer/Makefile
randomath/Makefile
mathml2ps/Makefile
])
